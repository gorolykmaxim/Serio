plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "org.serio"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                targets "serio-android"
            }
        }
    }
    externalNativeBuild {
        cmake {
            version "3.18.1"
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task buildUi(type: Exec) {
    workingDir "../../ui-web"
    commandLine "npm", "run", "build"
}

task clearUi(type: Delete, dependsOn: buildUi) {
    delete "src/main/assets"
}

task copyUi(type: Copy, dependsOn: clearUi) {
    from "../../ui-web/dist"
    into "src/main/assets"
}

preBuild.dependsOn copyUi

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
}